#!/usr/bin/perl -w
# -*- perl -*-

=head1 NAME

zetbox_facade_calls_ - Munin plugin to monitor the number of facade calls on a zetbox instance

=head1 CONFIGURATION

Link the plugin as zetbox_facade_calls_INSTANCENAME and set PERFMON_URL in the environment via plugins-conf.d

=head1 VERSION

  1.0.0

=head1 AUTHOR

Copyright (C) 2013 David Schmitt <david@dasz.at>

Based on the users plugin by Michael Kaiser tools (at) micha (dot) de and Nicolai Langfeldt.

=head1 LICENSE

GPLv2

=cut

use File::Basename;
use LWP::Simple;
use Munin::Plugin;

basename($0) =~ /^zetbox_facade_calls_(.*)$/;
my $instance = $1;

my %graphs = (
    # ORM calls
    'Queries' => {
        'colour' => 'FF0000',
        'draw'   => 'LINE2',
    },
    'SubmitChanges' => {
        'colour' => '00FF00',
        'draw'   => 'LINE2',
    },
    # API calls
    'FetchRelation' => {
        'colour' => '0000FF',
        'draw'   => 'AREA',
    },
    'GetList' => {
        'colour' => 'FFFF00',
        'draw'   => 'STACK',
    },
    'GetListOf' => {
        'colour' => '00FFFF',
        'draw'   => 'STACK',
    },
    'SetObjects' => {
        'colour' => 'FF00FF',
        'draw'   => 'STACK',
    },
);

my $fake_host = $ENV{'FAKE_HOST'};

if ( defined($ARGV[0]) ) {
    if ( $ARGV[0] eq "config" ) {
        print "graph_title zetbox total facade calls: $instance\n";
        print "graph_args --base 1000 -l 0\n";
        print "graph_vlabel calls/s\n";
        print "graph_category zetbox\n";
        if ($fake_host) {
			print "host_name ${fake_host}\n"
        }

        for my $graph (qw(FetchRelation GetList GetListOf SetObjects Queries SubmitChanges)) {
            print "${graph}Calls.label $graph calls\n";
            print "${graph}Calls.info number of $graph calls\n";
            print "${graph}Calls.draw $graphs{$graph}->{draw}\n";
            print "${graph}Calls.type DERIVE\n";
            print "${graph}Calls.min 0\n";
            print "${graph}Calls.colour $graphs{$graph}->{colour}\n";
            #print "${graph}Duration.label $graph duration\n";
            #print "${graph}Duration.info cumulative duration of $graph calls\n";
            #print "${graph}Duration.draw LINE1\n";
            #print "${graph}Duration.colour $graphs{$graph}->{colour}\n";
            #print "${graph}Objects.label $graph objects\n";
            #print "${graph}Objects.info number of returned objects from $graph calls\n";
            #print "${graph}Objects.draw LINE1\n";
            #print "${graph}Objects.colour $graphs{$graph}->{colour}\n";
        }
        print "ServerMethodInvocations.label method invocations\n";
        print "ServerMethodInvocations.info number of server method invocations\n";
        print "ServerMethodInvocations.draw LINE1\n";
        print "ServerMethodInvocations.type DERIVE\n";
        print "ServerMethodInvocations.min 0\n";
        print "ServerMethodInvocations.colour AAAAAA\n";
        exit 0;
    }
}

my $url = $ENV{'PERFMON_URL'};
my $counters = get($url) or die "Failure when retrieving data from $url: $!";
my %data = (
    'QueriesCalls' => 0,
    'SubmitChangesCalls' => 0,
    'FetchRelationCalls' => 0,
    'GetListCalls' => 0,
    'GetListOfCalls' => 0,
    'SetObjectsCalls' => 0,
    'ServerMethodInvocations' => 0,
);

for (split /\r?\n/, $counters) {
    next if /\/|AvgDuration/;
    next unless /Calls/;
    /^(.*): (\d*)$/;
    $data{$1} = $2;
}
for my $key (keys %data) {
    print "$key.value $data{$key}\n";
}
